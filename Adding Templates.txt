sphere() --> 

--

&CMD-SHEET/APPROVE #89=$sheet/approve *:

@eval 
	[setq(n, pmatch(%0))]
	[setq(m, get(%qn/_template))]
	[setq(s, sphere(%qn))]; 

@assert isstaff(%#)=
	{@pemit %#=Permission denied.}; 

@assert isdbref(%qn)=
	{@pemit %#=I can't find that player.}; 

@assert not(isapproved(%qn))=
	{@pemit %#=[name(%qn)]'s sheet is already approved.}; 

@set %qn=APPROVED !FIXED !UNINSPECTED; 

&_APPROVED %qn=%#|%k|[secs()]; 
&_WILLPOWER %qn=0; 
&_XP.DIFF.WEEKS %qn=u(#3088/data.xp.diff.weeks); 
&_TIER %qn=default(%qn/_TIER, 1); 

@mail/quick %qn/Approved: %xh%xr[name(%qn)]%xn=[ulocal(MAIL_APPROVAL, %qn)]; 

@eval game(%#, You approve [name(%qn)] to go IC.); 

@trigger %!/trig.approve.[edit(%qs, %b, _)]=%qn, %qm; 
@trigger %!/trig.rlevel.%qs/[edit(%qm, %b, _)]=%qn; 

@if t(setr(f, get(%qn/_FAMILY)))={
	@eval u(fct.faction.set, %qf, name(%qn), ); 
	@eval 
		u(#2832/c.faction/rank, 
			[name(%qn)]=
			%qf/
			[extract(extract(u(%qn/_MERITS), match(u(%qn/_MERITS), *%qf*, |), 1, |), 2, 1, ~)]
		)
}; 


-- SOME IMPORTANT DATA ---------------------------------------------------------

DATA.SUPERSTAT.TEMPLATES: Changeling:Wyrd/Normal Normal|Mage:Gnosis/Normal Normal|Promethean:Azoth/Normal Normal|Geist:Psyche/Fast Fast|Werewolf:Primal Urge/Normal Slow|Vampire:Blood Potency/Normal Slow


LIST_SPHERES: :|Werewolf:Werewolf Wolfblooded|Changeling:Changeling Fae-Touched|Vampire:Vampire Ghoul|Changing Breeds:Shifter|Geist:Geist|Mage:Mage Sleepwalker Proximus|Possessed:Possessed|Hunter:Hunter|Mortal+:Thaumaturge Psychic|Immortal:Body_Thief Blood_Bather Purified|Mortal:Mortal


-- GEIST ------------------------------------------------------------------

&trig.rlevel.geist/geist #89=
	@rxlevel %0=!All Real Twilight; @txlevel %0=!All Real Twilight

--

@@ There are no sub-templates in Geist ... right?!

&trig.approve.geist #89=
	&_psyche %0=max(1, get(%0/_psyche)); 
	&_plasm %0=u(fct.superstat.calc, %0); 

	@eval u(fct.faction.set, Geist, [name(%0)], [u(%0/_THRESHOLD)] [u(%0/_ARCHETYPE)])


-- CHANGELING -------------------------------------------------------------

&trig.rlevel.changeling/changeling #89=
	@rxlevel %0=!All Real Hedge; @txlevel %0=!All Real Hedge

--

&trig.rlevel.changeling/fae-touched #89=
	@rxlevel %0=!All Real Twilight; @txlevel %0=!All Real

--

&trig.approve.changeling #89=
	@trigger %!/trig.approve.changeling/%1=%0

--

&trig.approve.changeling/changeling #89=
	&_wyrd %0=max(1, get(%0/_wyrd)); 
	&_glamour %0=u(fct.superstat.calc, %0); 


	@eval u(fct.faction.set, Changeling, name(%0), get(%0/_court)); 

--

&trig.approve.changeling/fae-touched #89=
	@eval u(fct.faction.set, Changeling, name(%0), Fae-Touched); 
	&_glamour %0=u(fct.superstat.calc, %0); 


-- WEREWOLF ---------------------------------------------------------------

&trig.approve.werewolf #89=
	@trigger %!/trig.approve.werewolf/%1=%0

--

&trig.approve.werewolf/werewolf #89=

	&_primal-urge %0=max(1, get(%0/_primal-urge)); 
	&_essence %0=u(fct.superstat.calc, %0); 


	@trig %!/trig.species.werewolf=%0; 

	@eval setq(t, 
		iter(get(%0/_tribe), strtrunc(%i0, 1), _, @@)
	); 

	@eval setq(a, get(%0/_auspice)); 

	@eval u(fct.faction.set, Werewolf, name(%0), %qt %qa); 

--

&trig.approve.werewolf/wolfblooded #89=
	@eval u(fct.faction.set, Werewolf, name(%0), Wolfblooded); 

--

&trig.species.werewolf #89=&_SPECIES %0=Uratha:Hishu Dalu Gauru Urshul Urhan;



-- CHANGING BREEDS --------------------------------------------------------

&trig.approve.changing_breeds #89=
	@trigger %!/trig.approve.changing_breeds/%1=%0

--

&trig.approve.changing_breeds/shifter #89=
	&_feral-heart %0=max(1, get(%0/_feral-heart)); 
	&_essence %0=u(fct.superstat.calc, %0); 

	@eval setq(b, titlestr(edit(get(%qn/_BREED), _, %b))); 
	@eval setq(s, first(get(%qn/_SPECIES), :)); 

	@eval u(fct.faction.set, Changing Breeds, name(%qn), %qb - %qs); 

--

&trig.approve.changing_breeds/beastkin #89=
	@eval u(fct.faction.set, Changing Breeds, name(%0), Beastkin); 


-- MAGE ------------------------------------------------------------------

&trig.approve.mage #89=
	@trigger %!/trig.approve.mage/%1=%0

--

&trig.approve.mage/mage #89=
	&_gnosis %0=max(1, get(%0/_gnosis)); 
	&_mana %0=u(fct.superstat.calc, %0); 

	@eval u(fct.faction.set, Mage, name(%0), [u(%0/_PATH)] - [titlestr(edit(default(%0/_ORDER, Apostate), _, %b))]); 

--

&trig.approve.mage/proximus #89=
	@eval u(fct.faction.set, Mage, name(%0), Proximus - <dynasty>); 
	&_mana %0=u(fct.superstat.calc, %0); 

--

&trig.approve.mage/sleepwalker #89=
	@eval u(fct.faction.set, Mage, name(%0), Sleepwalker - [titlestr(edit(default(%qn/_ORDER, Apostate), _, %b))]); 


-- VAMPIRE ------------------------------------------------------------------

&trig.approve.vampire #89=
	@trigger %!/trig.approve.vampire/%1=%0

--

&trig.approve.vampire/vampire #89=
	&_blood-potency %0=max(1, get(%0/_blood-potency)); 
	&_vitae %0=u(fct.superstat.calc, %0); 

	@eval u(fct.faction.set, Vampire, name(%0), [u(%0/_CLAN)] - [edit(get(%0/_COVENANT), _, %b)]); 

--

&trig.approve.vampire/ghoul #89=
	@eval u(fct.faction.set, Vampire, name(%0), Ghoul); 
	&_vitae %0=u(fct.superstat.calc, %0); 


-- HUNTER ------------------------------------------------------------------

&trig.approve.hunter #89=
	@eval u(fct.faction.set, Mortal, name(%qn), ); 
	@eval u(fct.faction.set, Hunter, name(%qn), u(%qn/_CELL))


-- POSSESSED ------------------------------------------------------------------

&trig.approve.possessed #89=
	@eval u(fct.faction.set, Mortal, name(%0), ); 
	@eval u(fct.faction.set, Possessed, name(%0), u(%0/_VICE))


-- IMMORTAL ------------------------------------------------------------------

&trig.rlevel.immortal/purified #89=
	@rxlevel %0=!All Real Twilight; @txlevel %0=!All Real Twilight

--

&trig.approve.immortal #89=
	@trigger %!/trig.approve.immortal/%1=%0

--

&trig.approve.immortal/body_thief #89=
	@eval u(fct.faction.set, Immortal, [name(%0)], Body Thief); 

--

&trig.approve.immortal/blood_bather #89=
	@eval u(fct.faction.set, Immortal, [name(%0)], Blood Bather); 

--

&trig.approve.immortal/purified #89=
	&_chi %0=max(1, get(%0/_chi)); 
	&_essence %0=u(fct.superstat.calc, %0); 

	@eval u(fct.faction.set, Immortal, [name(%0)], Purified); 


-- MORTAL --------------------------------------------------------------------

&trig.approve.mortal #89=u(fct.faction.set, Mortal, name(%qn), )
&trig.approve.mortal+ #89=u(fct.faction.set, Mortal, name(%qn), )


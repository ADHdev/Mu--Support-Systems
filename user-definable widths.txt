/*
################################################################################
## User-Definable Window Width #################################################

This allows any player to manually set their width. I first coded this when 
trying out Windows Mu* clients which didn't always expand the visible window to 
the full size of the screen, and even when it did the code on the game didn't 
know how wide I had made my screen even if my client did.

That is, this is mainly for clients that don't report their NAWS correctly.

About NAWS: 
	Negotiate About Window Size, allows your client to report window 
	width and height. This is hella useful.

White Paper: 
	http://tools.ietf.org/html/rfc1073

Tested on: 
	MUX 2.12

Usage: 
	width( [<target>] )

Preferences: 
	&width me=<positive integer>



================================================================================
== <gamename>.conf =============================================================

function_name width width_old



================================================================================
== user-defined functions ======================================================

Make sure every user-defined function is built with at least @function/preserve


--------------------------------------------------------------------------------
-- the new 'width()' function --------------------------------------------------

*/

&UFUNC/PRIVILEGED.WIDTH <user functions object>=
	strcat( 
		setq( 0, if( t( %0 ), pmatch( %0 ), %# )), 
		if( 
			cor( 
				isstaff( %@ ), 
				haspower( %@, see_all ), 
				strmatch( %@, %q0 )
			), 

// calculate the real width
			strcat( 
				setq( u, get( %q0/width )), 
				setq( w, width_old( %q0 )), 
				case( 1, 

// .. (&width is manually set and correct, so use it)
					cand( isint( %qu ), gt( %qu, 0 )), 
					%qu, 

// .. (tinymux defaults to 78 if NAWS isn't passed; make it 79)
					eq( %qw, 78 ), 
					79, 

// .. (else, return the NAWS width)
					%qw 
				)
			), 

// fail if person without privs entered width( <someone not them> )
			#-1 Permission Denied 
		)
	)



/*
================================================================================
== Add with @function ==========================================================

Many games use #1/startup as a way to add important functions, so none drop off 
between restarts, crashes, reloads, and other situations. The raw command for 
this would be:

	@function/preserve/priviledged width=<user functions object>/UFUNC/PRIVILEGED.WIDTH

There is no reason to run this command more than once, or at every restart. If 
the new width code is changed, the function will reflect it immediately.



================================================================================
== then @restart ===============================================================

Finally, @restart to re-load the '<gamename>.conf' file to reload:

	@restart

Once you have restarted, you will not have to restart again unless you remove 
this function, in order to delete this function and restore the original 
width().

*/